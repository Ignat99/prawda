// **************************************************************************
//   Описание словообразования Эсперанто 
//   
// **************************************************************************

/*
# define W_SUB  @Стало{  сущ    Число=Ед Падеж=И }
# define W_ADJ  @Стало{  прил   Число=Ед Падеж=И }
# define W_VERB @Стало{  глагол Форма=Inf }
*/
@Переводики
{
// -------- формы слова -------------------------------------------------------------

   @Шаг { o      @Суффикс = o       o @Было {  сущ   Число=Ед Падеж=И } W_SUB }
   @Шаг { on     @Суффикс = on      o @Было {  сущ   Число=Ед Падеж=В } W_SUB }
   @Шаг { oj     @Суффикс = oj      o @Было {  сущ   Число=Мн Падеж=И } W_SUB }
   @Шаг { ojn    @Суффикс = ojn     o @Было {  сущ   Число=Мн Падеж=В } W_SUB }
   
   @Шаг { a      @Суффикс = a       a @Было {  прил  Число=Ед Падеж=И } W_ADJ }
   @Шаг { an     @Суффикс = an      a @Было {  прил  Число=Ед Падеж=В } W_ADJ }
   @Шаг { aj     @Суффикс = aj      a @Было {  прил  Число=Мн Падеж=И } W_ADJ }
   @Шаг { ajn    @Суффикс = ajn     a @Было {  прил  Число=Мн Падеж=В } W_ADJ }
   
   @Шаг { e      @Суффикс = e       e @Было { наречие } @Стало{ наречие } }
   
   @Шаг { i      @Суффикс = i       i @Было { глагол  Форма=Inf                    } W_VERB }
   @Шаг { as     @Суффикс = as      i @Было { глагол  Форма=Глагол Время=настоящее } W_VERB }
   @Шаг { is     @Суффикс = is      i @Было { глагол  Форма=Глагол Время=прошлое   } W_VERB }
   @Шаг { os     @Суффикс = os      i @Было { глагол  Форма=Глагол Время=будущее   } W_VERB }
   @Шаг { us     @Суффикс = us      i @Было { глагол  Форма=Услов                  } W_VERB }
   @Шаг { u      @Суффикс = u       i @Было { глагол  Форма=Повелит                } W_VERB }

   @Шаг { anta   @Суффикс = anta    i @Было { глагол  Форма=Причастие    Время=настоящее Число=Ед Падеж=И } W_VERB }
   @Шаг { antan  @Суффикс = antan   i @Было { глагол  Форма=Причастие    Время=настоящее Число=Ед Падеж=В } W_VERB }
   @Шаг { antaj  @Суффикс = antaj   i @Было { глагол  Форма=Причастие    Время=настоящее Число=Мн Падеж=И } W_VERB }
   @Шаг { antajn @Суффикс = antajn  i @Было { глагол  Форма=Причастие    Время=настоящее Число=Мн Падеж=В } W_VERB }
   @Шаг { inta   @Суффикс = inta    i @Было { глагол  Форма=Причастие    Время=прошлое   Число=Ед Падеж=И } W_VERB }
   @Шаг { intan  @Суффикс = intan   i @Было { глагол  Форма=Причастие    Время=прошлое   Число=Ед Падеж=В } W_VERB }
   @Шаг { intaj  @Суффикс = intaj   i @Было { глагол  Форма=Причастие    Время=прошлое   Число=Мн Падеж=И } W_VERB }
   @Шаг { intajn @Суффикс = intajn  i @Было { глагол  Форма=Причастие    Время=прошлое   Число=Мн Падеж=В } W_VERB }
   @Шаг { onta   @Суффикс = onta    i @Было { глагол  Форма=Причастие    Время=будущее   Число=Ед Падеж=И } W_VERB }
   @Шаг { ontan  @Суффикс = ontan   i @Было { глагол  Форма=Причастие    Время=будущее   Число=Ед Падеж=В } W_VERB }
   @Шаг { ontaj  @Суффикс = ontaj   i @Было { глагол  Форма=Причастие    Время=будущее   Число=Мн Падеж=И } W_VERB }
   @Шаг { ontajn @Суффикс = ontajn  i @Было { глагол  Форма=Причастие    Время=будущее   Число=Мн Падеж=В } W_VERB }

   @Шаг { ata    @Суффикс = ata     i @Было { глагол  Форма=Прич_пасс    Время=настоящее Число=Ед Падеж=И } W_VERB }
   @Шаг { atan   @Суффикс = atan    i @Было { глагол  Форма=Прич_пасс    Время=настоящее Число=Ед Падеж=В } W_VERB }
   @Шаг { ataj   @Суффикс = ataj    i @Было { глагол  Форма=Прич_пасс    Время=настоящее Число=Мн Падеж=И } W_VERB }
   @Шаг { atajn  @Суффикс = atajn   i @Было { глагол  Форма=Прич_пасс    Время=настоящее Число=Мн Падеж=В } W_VERB }
   @Шаг { ita    @Суффикс = ita     i @Было { глагол  Форма=Прич_пасс    Время=прошлое   Число=Ед Падеж=И } W_VERB }
   @Шаг { itan   @Суффикс = itan    i @Было { глагол  Форма=Прич_пасс    Время=прошлое   Число=Ед Падеж=В } W_VERB }
   @Шаг { itaj   @Суффикс = itaj    i @Было { глагол  Форма=Прич_пасс    Время=прошлое   Число=Мн Падеж=И } W_VERB }
   @Шаг { itajn  @Суффикс = itajn   i @Было { глагол  Форма=Прич_пасс    Время=прошлое   Число=Мн Падеж=В } W_VERB }
   @Шаг { ota    @Суффикс = ota     i @Было { глагол  Форма=Прич_пасс    Время=будущее   Число=Ед Падеж=И } W_VERB }
   @Шаг { otan   @Суффикс = otan    i @Было { глагол  Форма=Прич_пасс    Время=будущее   Число=Ед Падеж=В } W_VERB }
   @Шаг { otaj   @Суффикс = otaj    i @Было { глагол  Форма=Прич_пасс    Время=будущее   Число=Мн Падеж=И } W_VERB }
   @Шаг { otajn  @Суффикс = otajn   i @Было { глагол  Форма=Прич_пасс    Время=будущее   Число=Мн Падеж=В } W_VERB }

   @Шаг { ante   @Суффикс = ante    i @Было { глагол  Форма=Деепричастие Время=настоящее Число=Ед Падеж=И } W_VERB }
   @Шаг { inte   @Суффикс = inte    i @Было { глагол  Форма=Деепричастие Время=прошлое   Число=Ед Падеж=И } W_VERB }
   @Шаг { onte   @Суффикс = onte    i @Было { глагол  Форма=Деепричастие Время=будущее   Число=Ед Падеж=И } W_VERB }

// ----- переходы в другую часть речи -------------------------------------

@Шаг {   oa  @Суффикс = o a  @Было { сущ     } @Стало { прил    } @Переводик { sa_1 sa_2 sa_3 sa_4 } }
@Шаг {   oi  @Суффикс = o i  @Было { сущ     } @Стало { глагол  } @Переводик { sv1 /* (все sv) */ } }   
@Шаг 
{   anto @Суффикс = anto i  @Было { сущ     } @Стало { глагол  } @Переводик { sv1 /* (все sv) */ }
    @Перевод
    {  
       @Структура1 гр_сущ_п20( число Число, падеж Падеж ) =
		   глагол ( %Inf, @0, @0, @0 ) ;
       @Структура1 гр_сущ_п20( род &Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[тот]( Род, %О, Падеж, Число )
		   @0[,] @0[кто]
	           !глагол( %Несов, %глагол, %3е, %Ед, %настоящее, @0, %Актив, @0 ) ;
    }
}

@Шаг { oe  @Суффикс = o e  @Было { сущ     } @Стало { наречие }  /* все обратные ds */ }

@Шаг { ao  @Суффикс = a o  @Было { прил    } @Стало { сущ     } @Переводик { as1 as2 as3 } }
@Шаг { ai  @Суффикс = a i  @Было { прил    } @Стало { глагол  } @Переводик { sv } }
@Шаг { ae  @Суффикс = a e  @Было { прил    } @Стало { наречие } @Переводик { ad } }

@Шаг 
{   io  @Суффикс = i o  @Было { глагол  } @Стало { сущ     } /* @Переводик { обратный sv1 } */
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_п2( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   ?глагол[делать]( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж )
		   !сущ( &Р, &Д, %И, %Ед ) ;
    }
}
@Шаг 
{   ia  @Суффикс = i a  @Было { глагол  } @Стало { прил    }  /* Нет такой связки */
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_п3( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   ?глагол[быть]( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж )
		   !прил( %М, %Н, %Т, %Ед ) ;
    }        
}
@Шаг 
{   ie  @Суффикс = i e  @Было { глагол  } @Стало { наречие }  }
@Шаг 
{   eo  @Суффикс = e o  @Было { наречие } @Стало { сущ     }  
        @Переводик {  ds_1 ds_2 ds_3 ds_4 ds_5 ds_6 ds_7 ds_8 ds_9 ds_10 ds_11 ds_12 ds_13 ds_14 ds_15 ds_18 }  
           /* не могу придумать структуру */
	   /* как король */
}
@Шаг 
{   ea  @Суффикс = e a  @Было { наречие } @Стало { прил    }  @Переводик {  da /* обратное ad */  }  
           /* не могу придумать структуру */
}
@Шаг 
{   ei  @Суффикс = e i  @Было { наречие } @Стало { глагол  }  @Переводик {  dv  }  }

// ----- суффиксы ----------------------------------------------------------

// ACX  плохое качество, негодность, бездарность.
@Шаг
{  acx  @Суффикс = acxo o @Было { сущ    } @Стало { сущ    } 
   @Перевод
   {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	          сущ( &Число, &Падеж );
      @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                  ?прил[плохой]( Род, Душа, Падеж, Число )
	          !сущ( &Род, &Душа, Падеж, Число ) ;
   }
}
// AJX  (конкретное) вещь "красивый"->"украшение" "иметь"->"имущество"
@Шаг
{   ajx1 @Суффикс = ajxo a @Было { сущ    } @Стало { прил   } @Переводик { sa_1 /* и только оно */ } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п21( род %С, душа %Н, падеж &Падеж, число &Число ) =
                   ?сущ[свойство]( &Род, &Душа, Падеж, Число )
		   @0[быть]
		   !прил( %М, %Н, %Т, %Ед );
    }   
}
@Шаг
{   ajx2 @Суффикс = ajxo i @Было { сущ    } @Стало { глагол } }
// AN   член  группы, житель местности, приверженец  доктрины, особы которые принадлежат месту и т.п.                                                                        
@Шаг
{   an   @Суффикс = ano  o @Было { сущ    } @Стало { сущ    } @Переводик { ss4 ss4_1 } }
// AR   целое или собрание  многих samspecaj дел.                  
@Шаг
{   ar   @Суффикс = aro  o @Было { сущ    } @Стало { сущ    } @Переводик { ss11 } }
// CXJ  делают _ ласковые имена  мужчин.
@Шаг 
{   cxj  @Суффикс = cxjo o @Было { сущ    } @Стало { сущ    } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[милый]( Род, Душа, Падеж, Число )
	           !сущ( &Род, &Душа, Падеж, Число ) ;
    }
}
// EC   качество или состояние  кого-то или чего-то "краснота" "состояние краснота"
@Шаг
{   ec   @Суффикс = eco  o @Было { сущ    } @Стало { сущ    } }
// EG   увеличительный суффикс                                      
@Шаг 
{   eg   @Суффикс = ego  o @Было { сущ    } @Стало { сущ    } @Переводик { ss1 }
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[большой]( Род, Душа, Падеж, Число )
	           !сущ( &Род, &Душа, Падеж, Число ) ;
    }
}
// EJ   место, где делают это "учиться"->"школа"                    
@Шаг
{   ej   @Суффикс = ejo  i @Было { сущ    } @Стало { глагол } @Переводик { sv2_1 sv2_2 sv2_3  } }
// END  "необходимо, чтобы это было сделано"
@Шаг
{   end  @Суффикс = endo o @Было { сущ    } @Стало { сущ    } }
// ER   "маленькая часть чего-то"  "снег"->"снежинка"
@Шаг
{   er   @Суффикс = ero  o @Было { сущ    } @Стало { сущ    } @Переводик { ss10_1 } 
    @Перевод   // новая структура
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п12( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   прил[маленький]( %Ж, %Н, Падеж, Число )
	           ?сущ[часть]( &Род, &Душа, Падеж, Число )
		   !сущ( &Род1, &Душа1, %Р, %Ед ) ;
    }
}
// ESTR "руководитель чего-то" "империя"->"император"
@Шаг
{   estr @Суффикс = estro o @Было { сущ    } @Стало { сущ    }
    @Перевод   // новая структура
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п11( род Род, душа Душа, падеж &Падеж, число &Число ) =
	           ?сущ[руководитель]( &Род, &Душа, Падеж, Число )
		   !сущ( &Род1, &Душа1, %Р, %Ед ) ;
    }
}
// ET   уменьшительное значение "дом"->"домик"
@Шаг
{   et   @Суффикс = eto  o @Было { сущ    } @Стало { сущ    } @Переводик { ss2 ss2_1 } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[маленький]( Род, Душа, Падеж, Число )
	           !сущ( &Род, &Душа, Падеж, Число ) ;
    }
}
// ID   указывает на детеныша "собака"->"щенок"
@Шаг
{   id   @Суффикс = ido  o @Было { сущ    } @Стало { сущ    } @Переводик { ss3_1 } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п11( род Род, душа Душа, падеж &Падеж, число &Число ) =
	           ?сущ[детеныш]( &Род, &Душа, Падеж, Число )
	           !сущ( &Род1, &Душа1, %Р, %Ед ) ;
    }
}
// IL   инструмент, чтобы делать это "шить"->"игла"
@Шаг
{   il   @Суффикс = ilo  i @Было { сущ    } @Стало { глагол } @Переводик { sv2_1 sv2_2 sv2_3 } }
// IN   суффикс женского рода "пассажир"->"пассажирка"
@Шаг
{   in   @Суффикс = ino  o @Было { сущ    } @Стало { сущ    } @Переводик { ss3 } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п22( род %Ж, душа %О, падеж &Падеж, число &Число ) =
	           ?сущ[женщина]( &Род, &Душа, Падеж, Число )
	           !сущ( &Род1, &Душа1, %И, %Ед ) ;
    }
}
// ING  рукоятка, футляр или что-то во что часто кладут это "меч"->"ножны"
@Шаг
{   ing  @Суффикс = ingo o @Было { сущ    } @Стало { сущ    } }
// ISM  политическое движение "Сталин"->"сталинизм"
@Шаг
{   ism  @Суффикс = ismo o @Было { сущ    } @Стало { сущ    } }
// IST  персона, которая часто занимается этим (возможно профессионально) "лечить"->"врач"
@Шаг
{   ist  @Суффикс = isto i @Было { сущ    } @Стало { глагол } @Переводик { /* sv5_1 sv5_3 */ sv5_4 sv5_5 sv5_5a sv5_6 sv5_6a sv5_8 sv5_10 } }
// NJ   ласковые имена женщин
@Шаг
{   nj   @Суффикс = njo  o @Было { сущ    } @Стало { сущ    } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[милый]( Род, Душа, Падеж, Число )
	           !сущ( &Род, &Душа, Падеж, Число ) ;
    }
}
// UJ   что-то предназначенное для содержания чего-то "пепел"->"пепельница"
@Шаг
{   uj   @Суффикс = ujo  o @Было { сущ    } @Стало { сущ    } }
// UL   особа с какой-то характеристикой "толстый"->"толстяк"
@Шаг
{   ul   @Суффикс = ulo  a @Было { сущ    } @Стало {  прил  } }
// AD   длительное действие.
@Шаг
{   ad   @Суффикс = adi  i @Было { глагол } @Стало { глагол } 
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_п4( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   @0[долго] !?глагол( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж ) ;
    }   
}
// IG   действие приводящее к этому результату "острый"->"точить"
@Шаг
{   ig   @Суффикс = igi  a @Было { глагол } @Стало { прил   } 
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_п3( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   ?глагол[делать]( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж )
		   !прил( %М, %Н, %Т, %Ед );
    }
}
// IND  заслуживает быть сделанным "читать"->"следует быть прочитанным"
@Шаг {  ind  @Суффикс = indi i @Было { глагол } @Стало { глагол } }
// UM   нет определенного значения "лист"->"листать" "глаз"->"глазеть" "рука"->"обнимать"
@Шаг {  um   @Суффикс = umi  o @Было { глагол } @Стало {  сущ   } }
// IGX  переход к новому состоянию "острый"->"заостряться"
@Шаг
{   igx  @Суффикс = igxi a @Было { глагол } @Стало { прил   }
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_п3( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   ?глагол[делаться]( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж )
		   !прил( %М, %Н, %Т, %Ед );
    }   
}
// EBL  может быть сделанный.
@Шаг {  ebl  @Суффикс = ebla i @Было { прил   } @Стало { глагол } }
// EM   тенденция делать это "лгать"->"лживый"
@Шаг {  em   @Суффикс = ema  i @Было { прил   } @Стало { глагол } }
// OBL  суффикс умножения -  "два"->"двукратный" "двойной"
@Шаг {  obl  @Суффикс = obl - @Было { прил   } @Стало { числительное } }
// ON   суффикс деления      "два"->"половина"
@Шаг {  on   @Суффикс = on   - @Было { числительное } @Стало { числительное } }
// OP   группа с уверенным числом членов "десять"->"десятеро"
@Шаг {  op   @Суффикс = op   - @Было { числительное } @Стало { числительное } }

// ----- приставки ----------------------------------------------------------

// BO   = родство в результате брака "сын"->"зять"
@Шаг {  bo   @Приставка = bo   - @Было { сущ    } @Стало { сущ    } }
// CXEF = наиболее важный "епископ"->"архиепископ"
@Шаг
{   cxef @Приставка = cxef - @Было { сущ    } @Стало { сущ    } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[главный]( Род, Душа, Падеж, Число )
	           !сущ( &Род, &Душа, Падеж, Число ) ;
    }
}
// DIS  = действовать в разных направлениях "бежать"->"разбегаться"
@Шаг
{   dis  @Приставка = dis  - @Было { глагол } @Стало { глагол } }
// EK   = начала действия, внезапное действие "спать"->"заснуть"
@Шаг
{   ek   @Приставка = ek   - @Было { глагол } @Стало { глагол } 
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_п5( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   ?глагол[начать]( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж )
		   !глагол        ( %Несов, %Inf, @0, @0, @0, @0, @0, @0 ) ;
    }
}
// EKS  = обозначение бывшей роли "министр"->"эксминистр"
@Шаг
{   eks  @Приставка = eks  - @Было { сущ    } @Стало { сущ    }
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_п1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   ?прил[бывший]( Род, Душа, Падеж, Число )
	           !сущ( &Род, &Душа, Падеж, Число ) ;
    }   
}
// GE   = оба человека образующих пару "отец"->"родители"
@Шаг
{   ge   @Приставка = ge   - @Было { сущ    } @Стало { сущ    } }
// MAL  = противоположное значение "хороший"->"плохой"
@Шаг
{   mal_o  @Приставка = mal  - @Было { сущ    } @Стало { сущ    } 
    @Перевод
    {  @Структура1 гр_сущ_п0( число Число, падеж Падеж ) =
	           сущ( &Число, &Падеж );
       @Структура1 гр_сущ_н1( род Род, душа Душа, падеж &Падеж, число &Число ) =
                   @0[не] ?!сущ( &Род, &Душа, Падеж, Число ) ;
    }
}
@Шаг
{   mal_a  @Приставка = mal  - @Было { прил   } @Стало { прил   } @Переводик { aan  aa_5 } 
    @Перевод
    {  @Структура1 гр_прил_п0( число Число, падеж Падеж ) =
	           прил( &Число, &Падеж );
       @Структура1 гр_прил_п6( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
                   @0[не] ?!прил( Род, Душа, Падеж, Число ) ;
    }
}
@Шаг
{   mal_i  @Приставка = mal  - @Было { глагол } @Стало { глагол } 
    @Перевод
    {  @Структура1 гр_глагол_п0( форма Форма, время Время, число Число, падеж Падеж ) =
                   глагол( &Форма, &Время, &Число, &Падеж ) ;
       @Структура1 гр_глагол_н1( совершенность &СС, форма &Форма, лицо &Лицо, число &Число,
                                 время &Время, род &Род, залог &Залог, падеж &Падеж ) =
                   @0[не] ?!глагол( СС, Форма, Лицо, Число, Время, Род, Залог, Падеж ) ;
    }
}
@Шаг
{   mal_e  @Приставка = mal  - @Было { наречие} @Стало { наречие} 
    @Перевод
    {  @Структура1 гр_наречие_п0( смысл_наречия Смысл ) =
	           наречие( &Смысл );
       @Структура1 гр_наречие_н1( смысл_наречия Смысл ) =
                   @0[не] ?!наречие( &Смысл ) ;
    }
}
// MIS  = сделать что-то плохо или с ошибкой "шагать"->"оступаться"
@Шаг {  mis  @Приставка = mis  - @Было { глагол } @Стало { глагол } }
// PRA  = предыдущий очень долгого времени "человек"->"прачеловек"
@Шаг {  pra  @Приставка = pra  - @Было { сущ    } @Стало { сущ    } }
// RE   = делать это еще раз или в обратном направлении "читать"->"перечитывать" "давать"->"возвращать"
@Шаг {  re   @Приставка = re   - @Было { глагол } @Стало { глагол } }


// ------------------- главный алгоритм -----------------------
// ----------- используется один шаг без затей ----------------
@Алгоритм @главный
{  // --------- перехды в разные части речи -----------------
    1  0  oa   @Синт ;
    1  0  oi   @Синт ;
    1  0  anto @Синт ;
//  1  0  oe   @Синт ;
    1  0  ao   @Синт ;
    1  0  ai   @Синт ;
    1  0  ae   @Синт ;
//  1  0  io   @Синт ;
//  1  0  ia   @Синт ;
//  1  0  ie   @Синт ;
    1  0  eo   @Синт ;
    1  0  ea   @Синт ;
    1  0  ei   @Синт ;

    1  0  anto @Анал ;
    1  0  io   @Анал ;
    1  0  ia   @Анал ;

   // --------- существительные -----------------------------
//  1  0  acx   @Синт ;
    2  0  ajx1  @Синт ;
    4  0  an    @Синт ;
    5  0  ar    @Синт ;
//  6  0  cxj   @Синт ;
//  7  0  ec    @Синт ;
    8  0  eg    @Синт ;
//  9  0  end   @Синт ;
   10  0  er    @Синт ;
// 11  0  estr  @Синт ;
   12  0  et    @Синт ;
   13  0  id    @Синт ;
   14  0  il    @Синт ;
   15  0  in    @Синт ;
// 16  0  ing   @Синт ;
// 17  0  ism   @Синт ;
   18  0  ist   @Синт ;
// 19  0  nj    @Синт ;
// 20  0  uj    @Синт ;
// 21  0  ul    @Синт ;
  
// 22  0  bo    @Синт ;
// 23  0  cxef  @Синт ;
// 24  0  eks   @Синт ;
// 25  0  ge    @Синт ;
   26  0  mal_o @Синт ;
// 27  0  pra   @Синт ;

    1  0  acx   @Анал ;
    2  0  ajx1  @Анал ;
    6  0  cxj   @Анал ;
    8  0  eg    @Анал ;
   10  0  er    @Анал ;
   11  0  estr  @Анал ;
   12  0  et    @Анал ;
   13  0  id    @Анал ;
   19  0  nj    @Анал ;
   23  0  cxef  @Анал ;
   24  0  eks   @Анал ;
   26  0  mal_o @Анал ;

   // --------- глагол -----------------------------   
// 1  0  ad    @Синт ;
// 2  0  ig    @Синт ;
// 3  0  igx   @Синт ;
   4  0  ind   @Синт ;
// 5  0  dis   @Синт ;
// 6  0  ek    @Синт ;
// 7  0  mal_i @Синт ;
// 8  0  mis   @Синт ;
// 9  0  re    @Синт ;

   1  0  ad    @Анал ;
   2  0  ig    @Анал ;
   3  0  igx   @Анал ;
   6  0  ek    @Анал ;
   7  0  mal_i @Анал ;


   // --------- прилагательне ----------------------

// 1  0  ebl   @Синт ; (можно сделать аналитически)
// 2  0  em    @Синт ;
// 3  0  obl   @Синт ;
   3  0  mal_a @Синт ;

   3  0  mal_a @Анал ;

   // --------- наречие ----------------------------

// 7  0  mal_e @Синт ;

   7  0  mal_e @Анал ;
}

}
